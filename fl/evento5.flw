/* Kernighans large earthquake problem. */

// las fechas del archivo son MES/DIA/AÑO, y hopper solo trabaja con DIA/MES/AÑO
// por lo que se hace necesario hacer un SWAP para comprobar que las fechas son válidas
#include <flow.h>
#include <flow-flow.h>
#include <flow-regexp.h>

#define MAX_LINE                  1000
#define SwapDayByMonth(_N_,_M_)   TOK-SEP("/"), TOK(_N_), TOK-SWAP(_M_,Event), TOK-SEP(" ")

DEF-MAIN(argv,argc)
   MSET(fd, Event, T, rmatch ), VOID( Lines )
   TOK-INIT
   
   SET(flag, REG_EXTENDED)
   flag |= REG_NEWLINE
   flag |= REG_NOSUB
   SET(rmatch, "^([7-9]|6\.0*[1-9]).*$")
   REG-COMP(flag,rmatch)(T)

   OPEN-INPUT("datos.txt")(fd)
   COND( IS-NOT-FILE-ERROR? )
      TOK-SEP( " " ), VOID( Lines )
      TIC(t1)
      WHILE( NOT( EOF(fd) ) )
         LET( Event := USING(MAX_LINE) READ-LINE(fd) APPLY-TRM )
         WHEN( LEN(Event) ){
            WHEN( EQ?(TOK-COUNT( Event ),3) ){
               Swap Day By Month(1,2)
               WHEN( IS-DATE-VALID?( TOK(1) TOK-GET(Event) )){
                  {Event} PUSH( Lines )
                /*  TOK(3)
                  COND( REG-VALID(0, TOK-GET(Event), T) )
                     PRNL( Event )
                  CEND */
               }
            }
         }
      WEND
      CLOSE(fd), VOID( match, bools )
      TOK(3), REG-VALID(0, TOK-GET(Lines), T), CONV-TO-VAL, MUL-BY( Lines ), MOVE-TO( match ), FORGET //MOVE-TO( match )
      TOK-SEP(NL),PRNL( match )
      TOC( t1, t2 ), PRNL( "Time = ", t2 )
   ELS
      PRNL("Error: ", ~GET-STR-FILE-ERROR )
   CEND
   REG-FREE( T )
END
